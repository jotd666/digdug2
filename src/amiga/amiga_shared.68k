	.global osd_read_high_scores
	.global osd_write_high_scores
	.global	load_highscores
	.global	save_highscores
	.global osd_sound_start
	.global osd_music_stop
	.global	last_started_music_sound
	.global	handle_hurry_music_restart
	.global handle_music_termination


* sound engine and also highscore management, common to all versions
	
	.include "lvo/dos_lvos.i"
	.include "options.inc"
	.include "include.inc"
	.include "sounds.inc"

SCORE_FILE_SIZE = 0x200-0x1b0

		
osd_read_high_scores:
	tst.b	no_io_flag
	jeq		0f
	rts
0:
	movem.l	d0-d7/a0-a6,-(a7)
	move.l	_resload,d0
	beq.b	0f
	move.l	d0,a2
	bsr		get_hiscore_name
 	jsr		resload_GetFileSize(a2)		
	tst.l	d0
	beq.b	1f
	bsr		get_hiscore_name
	lea		high_score_buffer(pc),a1
	jsr		resload_LoadFile(a2)	
	bra.b	2f
0:
	tst.b	highscore_loaded
	beq.b	1f
2:
	* from DOS: just copy buffer
	GET_MEMORY_START_AND_DP
	* first just copy buffer as contiguous 

	lea		high_score_buffer(pc),a0
	lea	high_score_table_11b0(a6),a1
	move.w	#SCORE_FILE_SIZE-1,d0
0:
	move.b	(a0)+,(a1)+
	dbf		d0,0b

	* update high score (duplicated)
	lea		high_score_table_11b0(a6),a0
	lea		high_score_100b(a6),a1
	moveq	#2,d1
	* encoding is different
2:
	move.b	(a0)+,d0
	lsl.b	#4,d0
	or.b	(a0)+,d0
	move.b	d0,(a1)+
	dbf		d1,2b
1:
	movem.l	(a7)+,d0-d7/a0-a6
	rts
	
osd_write_high_scores:
	tst.b	no_io_flag
	jeq		0f
	rts
0:
	movem.l	d0-d7/a0-a6,-(a7)
	.ifdef	RELEASE
	tst.b	cheat_used		| don't save score if cheat on
	bne.b	1f
	.endif
	GET_MEMORY_START_AND_DP
	* just copy buffer as contiguous 

	lea		high_score_buffer(pc),a1
	lea	high_score_table_11b0(a6),a0
	move.w	#SCORE_FILE_SIZE-1,d0
0:
	move.b	(a0)+,(a1)+
	dbf		d0,0b

	st.b	highscore_needs_saving
1:
	
	move.l	_resload,d0
	beq.b	0f
	move.l	d0,a2
	move.l	#SCORE_FILE_SIZE,d0
	lea		high_score_buffer(pc),a1
	bsr		get_hiscore_name
	jsr		resload_SaveFile(a2)
0:

	movem.l	(a7)+,d0-d7/a0-a6
	rts

	
get_hiscore_name:
    lea normal_hiscorename,a0
	rts
	
load_highscores:
	bsr.b		get_hiscore_name
    move.l  _resload,d0
    bne.b   1f

    move.l  _dosbase,a6
    move.l  a0,d1
    move.l  #1005,d2
    jsr     (_LVOOpen,a6)
    move.l  d0,d1
    beq.b   1f
    move.l  d1,d4
    move.l  #SCORE_FILE_SIZE,d3
    move.l  #high_score_buffer,d2
    jsr (_LVORead,a6)
    move.l  d4,d1
    jsr (_LVOClose,a6)   
	st.b	highscore_loaded
1:

    rts

save_highscores:
	tst.l	_resload
	bne.b	1f
    tst.b   highscore_needs_saving
    beq.b   1f
    move.l  _dosbase,a6
	bsr.b		get_hiscore_name
    move.l  a0,d1
    move.l  #1006,d2
    jsr     (_LVOOpen,a6)
    move.l  d0,d1
    beq.b   1f
    move.l  d1,d4
    move.l  #SCORE_FILE_SIZE,d3
    move.l  #high_score_buffer,d2
    jsr (_LVOWrite,a6)
    move.l  d4,d1
    jsr (_LVOClose,a6)   
1:	
    rts
    

osd_music_stop:
*	movem.l	d0/a0/a6,-(a7)
*	lea		_custom,a6
*	move.w	#3,d0
*	jbsr	_mt_stopfx
*	movem.l	(a7)+,d0/a0/a6

	tst.b	music_playing
	jne		force_sound_stop
	rts

force_sound_stop:
	movem.l	d0-d1/a0/a6,-(a7)
	lea		_custom,a6
	clr.b	start_hurry_tune_timeout
	move.b	#-1,music_track_start_number
	clr.b	music_playing
	clr.b	looped_sound_enabled
    jsr		_mt_end
	
	move.w	last_started_music_sound,d0
	move.l	a5,d1		| save a5
	GET_MEMORY_START_AND_DP
	lea		(sound_level_start_tune_4040,a6),a0	
	clr.b	(a0,d0.w)		| memory address offset where to cancel the sound
	move.l	d1,a5
0:
	movem.l	(a7)+,d0-d1/a0/a6
	rts
	
* handling of tunes is quite a mess on that game.

osd_sound_start:
	movem.l	d0/d1/a0/a6,-(a7)
	lea		_custom,a6


	cmp.w	#HURRY_TUNE_SND,d0
	jne		0f
	st.b	lock_hurry_tune
	move.w	#WARNING_TUNE_SND,d0
0:

	lsl.w	#3,d0
	lea		sound_table,a0

	move.w	(a0,d0.w),d1	| sound type
	beq.b	11f		| no sound
	cmp.w	#3,d1
	jeq		10f		| skipped (normal)
2:
	cmp.w	#1,d1
	bne.b	4f		| music module
	move.w	(2,a0,d0.w),d1
	move.l	(4,a0,d0.w),a0
	lea		_custom,a6
	tst.w	d1
	beq.b	3f
	tst.b	looped_sound_enabled
	bne.b	3f
	* looped sound
	st.b	looped_sound_enabled
	jsr		_mt_loopfx
	bra.b	10f
3:
	jsr		_mt_playfx
10:
	* exiting osd_sound_start

	movem.l	(a7)+,d0/d1/a0/a6
	rts
11:
	move.w	d0,d1
	lsr.w	#3,d1

	BREAKPOINT	"unknown sound (index in D1)"
	jra		10b
	
	* music
4:
	* handle "lock" on hurry tune until some other tune plays
	cmp.w	#WARNING_TUNE_SND*8,d0
	jne		20f
	tst.b	lock_hurry_tune
	jeq		21f
	move.w	#HURRY_TUNE_SND*8,d0
	jra		21f
20:
	clr.b	lock_hurry_tune
21:

	move.w	(2,a0,d0.w),d1	| pattern number in .mod file

    movem.l d0-a6,-(a7)
    lea _custom,a6
 	movem.l	d0/a0,-(a7)
	lea	module_table,a0
	lsr.w	#1,d0		| divide by 2 as sound number was *8
	move.l	(a0,d0.w),a0
	.ifndef	RELEASE
	cmp.l	#0,a0
	jne		22f
	BREAKPOINT	"module not found for index d1"
22:
	.endif
	cmp.b	music_track_start_number(pc),d1
	jne	23f		| not the track, play
	cmp.l	current_module,a0
	jne	23f
	movem.l	(a7)+,d0/a0
	jra	5f	| same track, same module -> skip
23:
	* we don't force sound stop
	* it sets memory flags to 0 and triggers events too soon
	* (game over screen end, in-game music start)
	move.l	a0,current_module
	move.b	d1,music_track_start_number
	move.w	d1,d0
    sub.l   a1,a1		| samples are in the module
    jsr _mt_init
	
	* 2 first channels are reserved for the music
	* auto channel selection cannot pick them

	move	#3,d0
	jbsr	_mt_musicmask	
	
	movem.l	(a7)+,d0/a0

    * set master volume a little less loud if needed
	move.b	(6,a0,d0.w),music_volume
	move.b	(7,a0,d0.w),music_loops
	move.w	(2,a0,d0.w),music_pattern	| pattern number in .mod file
	move.w	(4,a0,d0.w),d2

	lsr.w	#3,d0
	move.w	d0,last_started_music_sound

    moveq	#0,d0
	move.b	music_volume(pc),d0
    jsr	_mt_mastervol
	st.b	music_playing
    jsr _mt_start
5:
    movem.l (a7)+,d0-a6
	
6:
	jra	10b

handle_hurry_music_restart:
	move.b	start_hurry_tune_timeout,d0
	jeq		0f
	subq.b	#1,d0
	jne		2f
	jbsr	osd_music_stop

	move.w	#HURRY_TUNE_SND,d0
	jbsr	osd_sound_start	
	moveq	#0,d0
2:
	move.b	d0,start_hurry_tune_timeout
0:
	rts
	
handle_music_termination:
	moveq	#0,d0
	move.b	_mt_E8Trigger,d0
	jeq		0f
	cmp.b	#4,d0
	jne		1f
	* command E8=4 asks to play the rest of "hurry" tune,
	* which is on a different module so we cannot even chain the tunes
	* no9 wasn't aware of that, I workarounded that but it's annoying
	*
	* problem is: game starts the "warning" tune over and over
	* so the "hurry" pattern cannot play, unless we tell the game
	* that this is the same thing: so don't clear the memory flag

	clr.b	_mt_E8Trigger	| ack
	move.b	#10,start_hurry_tune_timeout
	rts
1:
	* E8 command just issued from module, handle it
	* those commands are there to signal that some tune is
	* completed, game is actively waiting on some
	lea		music_trigger_address_table(pc),a0
	move.b	(a0,d0.w),d0
	
	move.l	a5,d1		| save a5
	GET_MEMORY_START_AND_DP
	lea		(sound_level_start_tune_4040,a6),a0
	clr.b	(a0,d0.w)		| memory address offset where to cancel the sound
	clr.b	_mt_E8Trigger	| ack
	move.l	d1,a5
0:
	rts
	
lock_hurry_tune:
	.byte	0
music_playing:
	.byte	0
looped_sound_enabled:
	.byte	0
music_loops:
	.byte	0
music_volume:
	.byte	0
start_hurry_tune_timeout:
	.byte	0
	.align	2
current_module:
	.long	0
music_track_start_number:
	.word	-1
music_pattern:
	.word	0
last_started_music_sound:
	.word	0

high_score_buffer:
	.skip	SCORE_FILE_SIZE
highscore_needs_saving:
	.byte	0
highscore_loaded:
	.byte	0

music_trigger_address_table:
	.byte	-1
	.byte	LEVEL_START_TUNE_SND	| trigger 81
	.byte	GAME_OVER_TUNE_SND     	| trigger 82
	.byte	HIGHSCORE_TUNE_SND     	| trigger 83
	.byte	WARNING_TUNE_SND       	| trigger 84
	.byte	LEVEL_COMPLETE_TUNE_SND | trigger 85
	.byte	CAUGHT_SND            	| trigger 86
	.byte	KILLED_SND            	| trigger 87
	.byte	-1               	 	| trigger 88
	
normal_hiscorename:
	.asciz	"digdug2.high"


